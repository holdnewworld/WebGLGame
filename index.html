<html>
  <head>
    <title>CS488 Project</title>
    <style>
      body { margin: 0; }
      canvas { width: 100%; height: 100% }
    </style>
  </head>
  <body>
    <script src="three.min.js"></script>
    <script type="text/javascript" src="dat.gui.min.js"></script>
    <script src="http://mrdoob.github.com/three.js/examples/fonts/helvetiker_regular.typeface.js"></script>
    
    <script type="x-shader/x-vertex" id="perlinVertexShader">
      varying vec3 coord;

      void main() {
        gl_Position = projectionMatrix*modelViewMatrix*vec4(position, 1.0);
        coord = position;
      }
    </script>
    <script type="x-shader/x-fragment" id="perlinFragmentShader">
      uniform float usingNoise;
      uniform vec3 base_color;
      varying vec3 coord;
      // Reference: http://blog.bombernauts.com/post/82310775255/perlin-noise-shader-tutorial
      float hash(float n) {
        return fract(sin(n)*43758.5453);
      }
      float noise(vec2 uv) {
        vec3 x = vec3(uv, 0);
        vec3 p = floor(x);
        vec3 f = fract(x);
        f = f*f*(3.0-2.0*f);
        float n = p.x + p.y*57.0 + 113.0*p.z;

        return mix(
          mix(mix( hash(n+0.0), hash(n+1.0),f.x),
            mix( hash(n+57.0), hash(n+58.0),f.x),f.y),
          mix(mix( hash(n+113.0), hash(n+114.0),f.x),
            mix( hash(n+170.0), hash(n+171.0),f.x),f.y),f.z);
      }
      float perlin_noise(vec2 uv) {
        float res = noise(uv    )*49.0
                  + noise(uv*2.0)*36.0
                  + noise(uv*4.0)*25.0
                  + noise(uv*8.0)*16.0
                  + noise(uv*16.0)*9.0
                  + noise(uv*32.0)*4.0
                  + noise(uv*64.0)*1.0;
        return res/(1.0+4.0+9.0+16.0+25.0+36.0+49.0);
      }

      void main() {
        vec2 noise3d = vec2(coord.x+coord.z, coord.y+coord.z);
        if (usingNoise > 0.5) {
          gl_FragColor = vec4(perlin_noise(noise3d/10.0))/2.0;
        } else {
          gl_FragColor = vec4(base_color, 1.0);
        }
      }
    </script>

    <script type="x-shader/x-vertex" id="textureVertexShader">
      varying vec2 textureCoord;

      void main() {
        textureCoord = uv;

        gl_Position = projectionMatrix*modelViewMatrix*vec4(position, 1.0);
      }
    </script>
    <script type="x-shader/x-fragment" id="textureFragmentShader">
      uniform float usingTextureMapping;
      uniform vec3 base_color;
      uniform sampler2D texture;
      varying vec2 textureCoord;

      void main() {
        if (usingTextureMapping > 0.5) {
          vec4 outColor = texture2D(texture, textureCoord);
          outColor.a = 0.3; // transparent when mouse over
          gl_FragColor = outColor;
        } else {
          gl_FragColor = vec4(base_color, 1.0);
        }
      }
    </script>

    <script type="x-shader/x-fragment" id="bumpFragmentShader">
      uniform float usingBumpMapping;
      uniform sampler2D normal_texture;
      uniform vec3 base_color;
      uniform vec3 light_pos;
      varying vec2 textureCoord;

      void main() {
        if (usingBumpMapping > 0.5) {
          vec3 normal = normalize(texture2D(normal_texture, textureCoord).rgb*2.0 - 1.0);
          vec3 normalized_light_pos = normalize(light_pos);
          float diffuse = max(dot(normal, normalized_light_pos), 0.05);
          vec3 color = 1.8 * diffuse * base_color;

          gl_FragColor = vec4(color, 1.0);
        } else {
          gl_FragColor = vec4(base_color, 1.0);
        }
      }
    </script>

    <script type="x-shader/x-vertex" id="mirrorVertexShader">
      uniform mat4 textureMatrix;
      varying vec4 mirrorCoord;

      void main() {
        vec4 mvPosition = modelViewMatrix * vec4( position, 1.0 );
        vec4 worldPosition = modelMatrix * vec4( position, 1.0 );
        mirrorCoord = textureMatrix * worldPosition;
        gl_Position = projectionMatrix * mvPosition;
      }
    </script>
    <script type="x-shader/x-fragment" id="mirrorFragmentShader">
      uniform vec3 mirrorColor;
      uniform sampler2D mirrorSampler;
      varying vec4 mirrorCoord;

      float blendOverlay(float base, float blend) {
        return( base < 0.5 ? ( 2.0 * base * blend ) : (1.0 - 2.0 * ( 1.0 - base ) * ( 1.0 - blend ) ) );
      }
      void main() {
        vec4 color = texture2DProj(mirrorSampler, mirrorCoord);
        color = vec4(blendOverlay(mirrorColor.r, color.r), blendOverlay(mirrorColor.g, color.g), blendOverlay(mirrorColor.b, color.b), 1.0);
        gl_FragColor = color;
      }
    </script>

    <script src="script.js"></script>
  </body>
</html>